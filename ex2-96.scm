(define (make-table)
  (let ((local-table (list '*table*)))
    (define (lookup key-1 key-2)
      (let ((subtable (assoc key-1 (cdr local-table))))
        (if subtable
            (let ((record (assoc key-2 (cdr subtable))))
              (if record
                  (cdr record)
                  false))
            false)))
    (define (insert! key-1 key-2 value)
      (let ((subtable (assoc key-1 (cdr local-table))))
        (if subtable
            (let ((record (assoc key-2 (cdr subtable))))
              (if record
                  (set-cdr! record value)
                  (set-cdr! subtable
                            (cons (cons key-2 value)
                                  (cdr subtable)))))
            (set-cdr! local-table
                      (cons (list key-1
                                  (cons key-2 value))
                            (cdr local-table)))))
      'ok)    
    (define (dispatch m)
      (cond ((eq? m 'lookup-proc) lookup)
            ((eq? m 'insert-proc!) insert!)
            (else (error "Unknown operation - TABLE" m))))
    dispatch))

(define operation-table (make-table))
(define get (operation-table 'lookup-proc))
(define put (operation-table 'insert-proc!))


(define (attach-tag type-tag content)
  (cons type-tag content))

(define (type-tag datum)
  (if (pair? datum)
      (car datum)
      (error "Bad tagged datum -- TYPE-TAG" datum)))

(define (contents datum)
  (if (pair? datum)
      (cdr datum)
      (error "Bad tagged datum - CONTENTS" datum)))

(define (add x y) (apply-generic 'add x y))
(define (sub x y) (apply-generic 'sub x y))
(define (mul x y) (apply-generic 'mul x y))
(define (div x y) (apply-generic 'div x y))
(define (greatest-common-divisor x y) (apply-generic 'greatest-common-divisor x y))
(define (reduce x y) (apply-generic 'reduce x y))

(define (apply-generic op . args)
  (let ((type-tags (map type-tag args)))
    (let ((proc (get op type-tags)))
      (if proc
          (apply proc (map contents args))
          (error
           "No method for these types --- APPLY-GENERIC"
           (list op type-tags))))))

(define (install-polynomial-package)
  (define (make-poly variable term-list)
    (cons variable term-list))
  (define (variable p) (car p))
  (define (term-list p) (cdr p))
  (define (variable? x) (symbol? x))
  (define (same-variable? v1 v2)
    (and (variable? v1) (variable? v2) (eq? v1 v2)))

  (define (adjoin-term term term-list)
    (if (=zero? (coeff term))
        term-list
        (cons term term-list)))
  (define (=zero? p)
    (if (eq? (type-tag p) 'number)
        (= (contents p) 0)
        (empty-termlist? (term-list p))))
  
  (define (the-empty-termlist) '())
  (define (first-term term-list) (car term-list))
  (define (rest-terms term-list) (cdr term-list))
  (define (empty-termlist? term-list) (null? term-list))
  
  (define (make-term order coeff) (list order coeff))
  (define (order term) (car term))
  (define (coeff term) (cadr term))

  (define (gcdtermlist L)
    (if (null? L)
        (make-number 0)
        (greatest-common-divisor (coeff (first-term L))
                                 (gcdtermlist (rest-terms L)))))
  (define (gcd-terms L1 L2)
    (if (empty-termlist? L2)
        L1
        (gcd-terms (car (div-terms L2 (list (list 0 (gcdtermlist L2)))))
                   (pseudoremainder-terms L1 L2))))
  (define (pseudoremainder-terms L1 L2)
    (let ((nL1 (mul-terms L1 (list (list 0 (make-number (expt
                                                         (cdr (coeff (first-term L2)))
                                                         (+ 1 (order (first-term L1))
                                                            (- (order (first-term L2)))))))))))
      (cadr (div-terms nL1 L2))))
  (define (gcd-poly p1 p2)
    (if (same-variable? (variable p1) (variable p2))
        (make-poly (variable p1)
                   (gcd-terms (term-list p1)
                              (term-list p2)))
        (error "Polys not in the same var -- GCD POLY"
               (list p1 p2))))
  (define (reduce-terms n d)
    (define gcd1 (gcd-terms n d))
    (define intfac (make-number (expt
                                 (cdr (coeff (first-term gcd1)))
                                 (+ 1 (max
                                       (order (first-term n))
                                       (order (first-term d)))
                                    (- (order (first-term gcd1)))))))
    (define n1 (car (div-terms (mul-terms n (list (list 0 intfac))) gcd1)))
    (define d1 (car (div-terms (mul-terms d (list (list 0 intfac))) gcd1)))
    (define gcdcoeffs (greatest-common-divisor (gcdtermlist n1) (gcdtermlist d1)))
    (list (car (div-terms n1 (list (list 0 gcdcoeffs))))
          (car (div-terms d1 (list (list 0 gcdcoeffs))))))
  (define (reduce-poly p1 p2)
    (if (same-variable? (variable p1) (variable p2))
        (cons (variable p1)
              (reduce-terms (term-list p1)
                            (term-list p2)))
        (error "Polys not in the same var -- REDUCE POLY"
               (list p1 p2))))
      
  
  (define (add-poly p1 p2)
    (if (same-variable? (variable p1) (variable p2))
        (make-poly (variable p1)
                   (add-terms (term-list p1)
                              (term-list p2)))
        (error "Polys not in same var -- ADD-POLY"
               (list p1 p2))))
  (define (add-terms L1 L2)
    (cond ((empty-termlist? L1) L2)
          ((empty-termlist? L2) L1)
          (else
           (let ((t1 (first-term L1)) (t2 (first-term L2)))
             (cond ((> (order t1) (order t2))
                    (adjoin-term
                     t1 (add-terms (rest-terms L1) L2)))
                   ((< (order t1) (order t2))
                    (adjoin-term
                     t2 (add-terms L1 (rest-terms L2))))
                   (else
                    (adjoin-term
                     (make-term (order t1)
                                (add (coeff t1) (coeff t2)))
                     (add-terms (rest-terms L1)
                                (rest-terms L2)))))))))
  
  (define (sub-poly p1 p2)
    (if (same-variable? (variable p1) (variable p2))
        (make-poly (variable p1)
                   (add-terms (term-list p1)
                              (neg-terms (term-list p2))))
        (error "Polys not in same var -- SUB-POLY"
               (list p1 p2))))
  (define (neg-terms L1)
    (mul-terms L1 (list (list 0 (make-number -1)))))
  
  (define (mul-poly p1 p2)
    (if (same-variable? (variable p1) (variable p2))
        (make-poly (variable p1)
                   (mul-terms (term-list p1)
                              (term-list p2)))
        (error "Polys not in same var -- MUL-POLY"
               (list p1 p2))))
  (define (mul-terms L1 L2)
    (if (empty-termlist? L1)
        (the-empty-termlist)
        (add-terms (mul-term-by-all-terms (first-term L1) L2)
                   (mul-terms (rest-terms L1) L2))))
  (define (mul-term-by-all-terms t1 L)
    (if (empty-termlist? L)
        (the-empty-termlist)
        (let ((t2 (first-term L)))
          (adjoin-term
           (make-term (+ (order t1) (order t2))
                      (mul (coeff t1) (coeff t2)))
           (mul-term-by-all-terms t1 (rest-terms L))))))

  (define (div-poly p1 p2)
    (if (same-variable? (variable p1) (variable p2))
        (list
         (make-poly (variable p1)
                    (car (div-terms (term-list p1)
                                    (term-list p2))))
         (make-poly (variable p1)
                    (cadr (div-terms (term-list p1)
                                     (term-list p2)))))
        (error "Polys not in same var -- DIV-POLY"
               (list p1 p2))))
  (define (div-terms L1 L2)
    (if (empty-termlist? L1)
        (list (the-empty-termlist) (the-empty-termlist))
        (let ((t1 (first-term L1))
              (t2 (first-term L2)))
          (if (> (order t2) (order t1))
              (list (the-empty-termlist) L1)
              (let ((new-c (div (coeff t1) (coeff t2)))
                    (new-o (- (order t1) (order t2))))
                (let ((rest-of-result
                       (div-terms
                        (add-terms L1
                                   (neg-terms (mul-terms L2
                                                         (list (make-term new-o new-c)))))
                        L2)))
                  (list (adjoin-term (make-term new-o new-c)
                                     (car rest-of-result))
                        (cadr rest-of-result))))))))
  
  (define (tag p) (attach-tag 'polynomial p))
  (put 'add '(polynomial polynomial)
       (lambda (p1 p2) (tag (add-poly p1 p2))))
  (put 'sub '(polynomial polynomial)
       (lambda (p1 p2) (tag (sub-poly p1 p2))))
  (put 'mul '(polynomial polynomial)
       (lambda (p1 p2) (tag (mul-poly p1 p2))))
  (put 'div '(polynomial polynomial)
       (lambda (p1 p2) (attach-tag '(polynomial polynomial)
                                   (list (car (div-poly p1 p2)) (cadr (div-poly p1 p2))))))
  (put 'greatest-common-divisor '(polynomial polynomial)
       (lambda (p1 p2) (tag (gcd-poly p1 p2))))
  (put 'reduce '(polynomial polynomial)
       (lambda (p1 p2) (attach-tag '(polynomial polynomial) (reduce-poly p1 p2))))
  (put 'make 'polynomial
       (lambda (var terms) (tag (make-poly var terms))))
  (put 'zero '(polynomial) =zero?)
  'done)

(define (make-polynomial var terms)
  ((get 'make 'polynomial) var terms))

(define (install-number-package)
  (define (reduce-integers n d)
    (let ((g (gcd n d)))
      (list (/ n g) (/ d g))))
  (define (tag x)
    (attach-tag 'number x))
  (put 'add '(number number)
       (lambda (x y) (tag (+ x y))))
  (put 'sub '(number number)
       (lambda (x y) (tag (- x y))))
  (put 'mul '(number number)
       (lambda (x y) (tag (* x y))))
  (put 'div '(number number)
       (lambda (x y) (tag (/ x y))))
  (put 'greatest-common-divisor '(number number)
       (lambda (x y) (tag (gcd x y))))
  (put 'make 'number
       (lambda (x) (tag x)))
  (put 'reduce '(number number)
       (lambda (x y) (attach-tag '(number number) (reduce-integers x y))))
  'done)

(define (install-rational-package)
  (define (numer x) (car x))
  (define (denom x) (cdr x))
  (define (make-rat n d)
    (cond ((equal? (type-tag (reduce n d)) '(polynomial polynomial))
           (cons (make-polynomial (car (contents n)) (cadr (contents (reduce n d))))
                 (make-polynomial (car (contents n)) (caddr (contents (reduce n d))))))
          ((equal? (type-tag (reduce n d)) '(number number))
           (cons (car (contents (reduce n d)))
                 (cadr (contents (reduce n d)))))
          (else error "Type tag not recognised -- MAKE-RAT"
                (list n d))))
  (define (add-rat x y)
    (make-rat (add (mul (numer x) (denom y))
                   (mul (numer y) (denom x)))
              (mul (denom x) (denom y))))
  (define (sub-rat x y)
    (make-rat (sub (mul (numer x) (denom y))
                   (mul (numer y) (denom x)))
              (mul (denom x) (denom y))))
  (define (mul-rat x y)
    (make-rat (mul (numer x) (numer y))
              (mul (denom x) (denom y))))
  (define (div-rat x y)
    (make-rat (mul (numer x) (denom y))
              (mul (denom x) (numer y))))

  (define (tag x) (attach-tag 'rational x))
  (put 'add '(rational rational)
       (lambda (x y) (tag (add-rat x y))))
  (put 'sub '(rational rational)
       (lambda (x y) (tag (sub-rat x y))))
  (put 'mul '(rational rational)
       (lambda (x y) (tag (mul-rat x y))))
  (put 'div '(rational rational)
       (lambda (x y) (tag (div-rat x y))))
  (put 'make 'rational
       (lambda (n d) (tag (make-rat n d))))
  'done)

(define (make-rational n d)
  ((get 'make 'rational) n d))

(define (make-number n)
  ((get 'make 'number) n))

(install-number-package)
(install-rational-package)
(install-polynomial-package)
